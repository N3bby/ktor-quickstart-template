plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
}

group 'com.razacx'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

ext.MainClass = 'com.razacx.project.ApplicationKt'

ext.ktor_version = '1.1.3'
ext.koin_version = '2.0.0-rc-2'
ext.jackson_version = '2.9.7'
ext.junit_version = '5.3.1'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Ktor
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"

    // Jackson
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    // Logging
    compile "ch.qos.logback:logback-classic:1.2.3"

    // Koin
    compile "org.koin:koin-core:$koin_version"
    compile "org.koin:koin-ktor:$koin_version"
    compile "org.koin:koin-logger-slf4j:$koin_version"
    testCompile "org.koin:koin-test:$koin_version"

    // Persistence
    compile 'org.jetbrains.exposed:exposed:0.10.4'
    compile 'com.h2database:h2:1.4.197'

    // JUnit + Testing libs
    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testCompile "org.assertj:assertj-core:3.12.2"
    testCompile "io.mockk:mockk:1.9.3"

}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': "$MainClass"
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
